buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
	}
}

repositories {
	maven {
		url = "http://repo.spongepowered.org/maven/"
	}
}

apply plugin: "forge"

ext.baseVersion = "2.1.4"
version = baseVersion
group = "com.darkshadow44"
archivesBaseName = "lightoverhaul"
sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
	version = "1.7.10-10.13.4.1558-1.7.10"
	runDir = "rundir"
}

configurations {
	shade
	shadeClient
	buildDep
	compile.extendsFrom buildDep, shade, shadeClient
}

dependencies {
	shade group: "org.spongepowered", name: "mixin", version: "0.7-SNAPSHOT", changing: true
	shadeClient group: "org.spongepowered", name: "mixin", version: "0.7-SNAPSHOT", changing: true
}

def refMap = "${tasks.compileJava.temporaryDir}" + File.separator + "mixins.lightoverhaul.refmap.json"

jar {
	appendix = minecraft.version

	from refMap

	configurations.shade.copyRecursive().setTransitive(false).each { artifact ->
		from (zipTree(artifact))
	}

	manifest {
		attributes (
			"FMLCorePluginContainsFMLMod": "true",
			"FMLCorePlugin": "coloredlightscore.src.asm.ColoredLightsCoreLoadingPlugin",
			"ForceLoadAsMod": "true",
			"TweakOrder": "0",
			"MixinConfigs": "mixins.lightoverhaul.json",
			"TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
		)
	}
}

def mixinSrg = "${tasks.reobf.temporaryDir}" + File.separator + "mixins.srg"

reobf {
	addExtraSrgFile mixinSrg
}

afterEvaluate {
	tasks.compileJava.options.compilerArgs += ["-AreobfSrgFile=${tasks.reobf.srg}", "-AoutSrgFile=${mixinSrg}", "-AoutRefMapFile=${refMap}"]
}
